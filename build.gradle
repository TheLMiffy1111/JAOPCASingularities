plugins {
	id "com.crystaelix.loom" version "jitpack-f4853e7a"
	id "eclipse"
	id "idea"
	id "maven-publish"
	id "net.kyori.blossom" version "[1,2)"
	id "me.modmuss50.mod-publish-plugin" version "0.5.0"
}

repositories {
	mavenCentral()
	flatDir {
		dirs "libs"
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.blamejared.com/"
			}
		}
		filter {
			includeGroup "mezz.jei"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.covers1624.net/"
			}
		}
		filter {
			includeGroup "codechicken"
			includeGroup "morph.avaritia"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://repo.cleanroommc.com/releases/"
			}
		}
		filter {
			includeGroup "zone.rong"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://cursemaven.com/"
			}
		}
		filter {
			includeGroup "curse.maven"
		}
	}
}

sourceSets {
	main
}

def mcVersion = "1.12.2"
def modName = "JAOPCASingularities"
def modId = "jaopcasingularities"
def specVersion = "3"
def vendor = "thelm"

group = "thelm.jaopca.singularities"
version = "3.0.1.1"

archivesBaseName = "${modName}-${mcVersion}"

java {
	toolchain.languageVersion = JavaLanguageVersion.of(8)
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

loom {
	silentMojangMappingsLicense()
	interfaceInjection.enableDependencyInterfaceInjection = false
	runs {
		configureEach {
			property "fabric.log.console.level", "debug"
			environmentVariable "MC_VERSION", mcVersion
		}
		client {
			programArg "--username=Dev"
			vmArg "-Xmx7G"
		}
		server {
			vmArg "-Xmx3G"
		}
	}
	generatedIntermediateMappings()
}

dependencies {
	// Log4j
	implementation "org.apache.logging.log4j:log4j-core:2.22.1"

	// Minecraft
	minecraft "com.mojang:minecraft:1.12.2"
	mappings "de.oceanlabs.mcp:mcp_stable:39-1.12"
	legacyForge "net.minecraftforge:forge:1.12.2-14.23.5.2860"

	// Dependencies
	implementation "curse.maven:wrapup-275153:2486198"
	modImplementation "curse.maven:jaopca-266936:5126036"
	modImplementation "codechicken:CodeChickenLib:1.12.2-3.2.3.358:universal"
	modImplementation("morph.avaritia:Avaritia:1.12.2-3.3.0.37:universal") {transitive = false}
	modImplementation "curse.maven:wanionlib-253043:4623135"
	modImplementation "curse.maven:eternal-singularity-253077:2922583"
	modImplementation "mezz.jei:jei_1.12.2:4.16.1.1012"

	// Testing
	modImplementation "curse.maven:abyssalcraft-53686:4856547"

	// Performance
	implementation("zone.rong:mixinbooter:9.1") {transitive = false}
	modImplementation "curse.maven:reid-629017:5143492"
	modImplementation "curse.maven:vintagefix-871198:5010926"
	modImplementation "curse.maven:lolasm-460609:5120166"
}

def replacements = [
	"0@VERSION@": version
]

blossom {
	replaceToken(replacements)
}

processResources {
	inputs.properties(replacements)
	filesMatching(["mcmod.info", "META-INF/mods.toml"]) {
		filter {
			it.replace(replacements)
		}
	}
}

def manifestAttributes = [
	"Specification-Title": modName,
	"Specification-Vendor": vendor,
	"Specification-Version": specVersion,
	"Implementation-Title": archivesBaseName,
	"Implementation-Version": version,
	"Implementation-Vendor": vendor
]

jar {
	manifest.attributes(manifestAttributes)
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}

publishMods {
	file = remapJar.archiveFile
	version = displayName = "${mcVersion}-${project.version}"
	changelog = file("changelog.md").text
	type = BETA
	modLoaders.add("forge")
	additionalFiles.from(remapSourcesJar.archiveFile)

	curseforge {
		accessToken = providers.gradleProperty("curseforge.token")
		projectId = "268610"
		minecraftVersions.add(mcVersion)
		requires "jaopca", "avaritia-1-10"
		optional "eternal-singularity"
	}

	modrinth {
		accessToken = providers.gradleProperty("modrinth.token")
		projectId = "O9qb04PW"
		minecraftVersions.add(mcVersion)
		requires "jaopca"
	}
}
